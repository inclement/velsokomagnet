title History of the Velsokomagnet
author inclement
homepage www.puzzlescript.net

require_player_movement
key_repeat_interval 0.1
run_rules_on_level_start

========
OBJECTS
========

Nothing
black

Background
#eeeeee #eeeeee
01000
01000
10011
00100
01000

Target
#c9c9c9 #eeeeee green
01010
10001
00200
10001
01010

PlayerTarget
#c9c9c9 darkgreen green
.0.0.
00000
.020.
00000
.0.0.

Wall
#a59aa5 #bb77bb
00000
00000
00000
00000
00000

WallBase
#a59aa5 #bb77bb #aa9daa
00000
00000
00000
00000
11111

Player
#0055ee
.0.0.
00000
.000.
00000
.0.0.


Crate
#ee8822
0.0.0
.000.
00000
.000.
0.0.0

PlayerCrateRightMarker
#ee8822
....0
.....
....0
.....
....0

PlayerCrateAboveMarker
#ee8822
0.0.0
.....
.....
.....
.....

PlayerCrateLeftMarker
#ee8822
0....
.....
0....
.....
0....

PlayerCrateBelowMarker
#ee8822
.....
.....
.....
.....
0.0.0

CratePlayerRightMarker
#0055ee
.....
....0
.....
....0
.....

CratePlayerAboveMarker
#0055ee
.0.0.
.....
.....
.....
.....

CratePlayerLeftMarker
#0055ee
.....
0....
.....
0....
.....

CratePlayerBelowMarker
#0055ee
.....
.....
.....
.....
.0.0.

MovedCrateMarker
pink
00000
0...0
0...0
0...0
00000

CrateOnTargetMarker
lightgreen
.....
.....
..0..
.....
.....

PlayerOnTargetMarker
lightgreen
.....
.....
..0..
.....
.....

MovedPlayerMarker
red

NextPlayerPositionMarker
purple


IntroCrate
#ee8822 #cc6600
.000.
01110
00000
01110
.000.

IntroCrateTarget
#c9c9c9 #eeeeee green
10001
00000
00200
00000
10001

IntroPlayerTarget
#c9c9c9 #eeeeee green
11111
11111
11211
11111
11111



=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = Crate
@ = Crate and Target
O = Target
x = PlayerTarget
q = Player and PlayerTarget
z = Crate and PlayerTarget
N = Nothing
i = IntroCrate
j = IntroCrateTarget

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Nothing
Target, PlayerTarget, IntroCrateTarget, IntroPlayerTarget

PlayerCrateRightMarker
PlayerCrateLeftMarker
PlayerCrateAboveMarker
PlayerCrateBelowMarker
CratePlayerRightMarker
CratePlayerAboveMarker
CratePlayerLeftMarker
CratePlayerBelowMarker

Player, Wall, Crate, IntroCrate
WallBase  ( Note WallBase drawn on top of Wall as it's just decoration )
MovedPlayerMarker, MovedCrateMarker, NextPlayerPositionMarker

CrateOnTargetMarker, PlayerOnTargetMarker

======
RULES
======

( Normal sokoban rules for the introductory level only )

[ > Player | IntroCrate ] -> [ > Player | > IntroCrate ]
late [IntroCrate IntroCrateTarget no CrateOnTargetMarker] -> [IntroCrate IntroCrateTarget CrateOnTargetMarker ]
late [IntroCrate IntroCrateTarget no CrateOnTargetMarker] -> [IntroCrate IntroCrateTarget CrateOnTargetMarker ]


( Velsokomagnet rules for other levels )


[ > Player | Crate ] -> [ > Player | > Crate > MovedCrateMarker ]
[ ^ Player | Crate ] -> [ ^ Player | ^ Crate ^ MovedCrateMarker ]
[ < Player | Crate ] -> [ < Player | < Crate < MovedCrateMarker ]
[ v Player | Crate ] -> [ v Player | v Crate v MovedCrateMarker ]
[ > Crate | Crate ] -> [ > Crate | > Crate ]
[ > MovedCrateMarker | Wall ] -> cancel

[ > Player | ] -> [ > Player | NextPlayerPositionMarker ]
[ NextPlayerPositionMarker | Crate ] -> [NextPlayerPositionMarker | Crate MovedCrateMarker ]
[ NextPlayerPositionMarker | ... | Crate no MovedCrateMarker ] -> [ NextPlayerPositionMarker | ... | > Crate ]

( Mark joining when box/player are adjacent )
[> Player PlayerCrateRightMarker ] -> [> Player > PlayerCrateRightMarker ]
[> Player PlayerCrateLeftMarker ] -> [> Player > PlayerCrateLeftMarker ]
[> Player PlayerCrateAboveMarker ] -> [> Player > PlayerCrateAboveMarker ]
[> Player PlayerCrateBelowMarker ] -> [> Player > PlayerCrateBelowMarker ]
[> Crate CratePlayerRightMarker ] -> [> Crate > CratePlayerRightMarker ]
[> Crate CratePlayerLeftMarker ] -> [> Crate > CratePlayerLeftMarker ]
[> Crate CratePlayerAboveMarker ] -> [> Crate > CratePlayerAboveMarker ]
[> Crate CratePlayerBelowMarker ] -> [> Crate > CratePlayerBelowMarker ]


late [ MovedCrateMarker ] -> [ ]
late [ NextPlayerPositionMarker ] -> [ ]

( Mark when crates or the player are on their target )
late [Player PlayerTarget no PlayerOnTargetMarker] -> [Player PlayerTarget PlayerOnTargetMarker]
late [PlayerOnTargetMarker no Player] -> [ ]
late [Crate Target no CrateOnTargetMarker] -> [Crate Target CrateOnTargetMarker]
late [CrateOnTargetMarker no Crate no IntroCrate] -> [ ]

late DOWN [ Wall | no Wall ] -> [WallBase Wall | ]
late Down [ WallBase | Wall ] -> [ | Wall ]

late RIGHT [ Player no PlayerCrateRightMarker | Crate ] -> [ Player PlayerCrateRightMarker | Crate CratePlayerLeftMarker]
late UP    [ Player no PlayerCrateAboveMarker | Crate ] -> [ Player PlayerCrateAboveMarker | Crate CratePlayerBelowMarker]
late LEFT  [ Player no PlayerCrateLeftMarker | Crate ] ->  [ Player PlayerCrateLeftMarker | Crate CratePlayerRightMarker ]
late DOWN  [ Player no PlayerCrateBelowMarker | Crate ] -> [ Player PlayerCrateBelowMarker | Crate CratePlayerAboveMarker ]


( late [ stationary MovedCrateMarker ] -> [ ]  )
( late [ stationary MovedPlayerMarker ] -> [ ] )

==============
WINCONDITIONS
==============

all Target on Crate
all Player on PlayerTarget
all IntroCrateTarget on IntroCrate

=======
LEVELS
=======

( nnn###########nnn )
( nnn####...####nnn )
( nnn#.........#nnn )
( ####.........#### )
( #..o....***...o.# )
( ####.......p.#### )
( nnn#....o....#nnn )
( nnn#####x#####nnn )
( nnn###########nnn )

(Normal sokoban introduction)

message (Intro 1 / 2) Sokoban was invented when a global surplus of crates and targets led to a new form of warehouse management.

nnnnn#####nnn
nnnnn#iii#nnn
######i..#nnn
####i....#nnn
#i.....#.#nnn
#ip.#i.#.###n
#i.....#.i##n
########.i##n
nnnn#.....i#n
nnnn#.j...x#n
nnnn#iii.###n
nnnn########n

message (Intro 2 / 2) Later advances in materials science introduced the sokomagnetic force that repelled the crates...

message (Intro 2 / 2) ...also the warehouse contents were covered in velcro for storage convenience, after an unfortunate incident involving the lead engineer's car keys.

(Basic introduction)

nnnnn#####nnn
nnnnn#...#nnn
######...#nnn
####.....#nnn
#......#.#nnn
#.p.#*.#.###n
#......#..##n
########..##n
nnnn#......#n
nnnn#.o...x#n
nnnn#....###n
nnnn########n

message (1 / 10) The resulting player entity became known as...the VELSOKOMAGNET.

(Easy 3)
###########
#.........#
#.........#
#..*......#
#...o...x.#
#....o....#
#.....*.#.#
#.........#
#..p......#
###########

message (2 / 10) The velsokomagnet quickly became popular, since it made crate arrangement so much easier than before.


(Easy 2)
##########
#........#
#..#.....#
#..z..o..#
#.....#..#
#..*.....#
#..#.....#
#p..o....#
##########

message (3 / 10) It was initially considered quite gauche to use the velcro to trivialise the crate management.

(Easy 1)
##########
#q.....###
#........#
#.#*.#...#
#.#.O.#..#
#....*...#
#..#.#...#
#.O....#.#
##########

message (4 / 10) Over time attitudes changed and some warehouse designers began relying on the velcro just as much as the sokomagnetic repulsion.

(Velcro easy introduction)
nnnn#####nn
nnnn#...#nn
#####.#.#nn
###.#.#.#nn
##.o.*..#nn
#.ox#.#.###
##.......##
#.p###.#.##
#.#..*....#
#.........#
######.#..#
nnnnn#...##
nnnnn######

message (5 / 10) Even during the height of the velsokomagnet boom, the most popular warehouses eschewed relying on the velcro.

(Easy 4)
#########nnnnnn
##.....##nnnnnn
##.#...########
##......#.....#
##..#*..#...x.#
##............#
#.....*.......#
#.......#.....#
#.......#.opo.#
#.......#.....#
###############

message (6 / 10) The next warehouse is the second in a twin set commemorating the 10th anniversary of the sokoban warehouse.

(Easy 5)
#########nnnnnn
##.....##nnnnnn
##...#.##nnnnnn
##.#*..########
##......#.....#
##..#*..#...x.#
##............#
#.....*.......#
#.......#...o.#
#.......#.opo.#
#.......#.....#
###############

message (7 / 10) During its heights, the velsokomagnet's popularity eclipsed more traditional warehouse variants.

(Velcro easy 1)
nnnnn#######n
nnnnn#.....#n
nnnnn#.....#n
#######..#.#n
#.......*..#n
#...##..#..#n
#......###.##
####...###.o#
nnn#...###.x#
nnn#.*..##.##
nnn#.....#.#n
nnn#....p.o#n
nnn####....#n
nnnnnn###..#n
nnnnnnnn####n

message (8 / 10) The eventual downfall of the velsokomagnet was not due to disinterest, but economics.

(Medium? 1)
(TODO: This has a solution fail if one crate is left in the trap while the others are arranged)
nnnn######nn
nnnn#...##nn
#####.#..#nn
##o##..#.###
#o..x*#...##
###o#......#
###.#..*.#.#
#.......##.#
#..*...###.#
#.p...####.#
##...#####.#
############

message (9 / 10) The materials used to create stable sokomagnetic poles became more valuable in other industries, making velsokomagnetic warehouses expensive to maintain.


(Velcro easy 2)

nnn###########nnn
nnn####...####nnn
nnn#.........#nnn
####.........####
#..o....***...o.#
####.......p.####
nnn#....o....#nnn
nnn#####x#####nnn
nnn###########nnn

message (10 / 10) In the end velsokomagnetic warehouses were financially unsustainable. Public interest moved on, and the velsokomagnet is now mostly confined to the annals of history.

message (10 / 10) This last example was the winner of a public competition, demonstrated at the world warehouse design expo using one of the last remaining velsokomagnets.

(Medium 1 - not fully tested for simpler solutions )
###########
#.....p...#
#.o#..###.#
#.........#
#..#..#.#.#
#...#..*..#
##.....#..#
n#.#.*..#.#
n#..#.....#
n#..#*...x#
n#..o....o#
n#.o....#.#
n#....###*#
n##########

message That concludes this history of the velsokomagnetic era in sokoban warehouse arrangement. Any questions?
